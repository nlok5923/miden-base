use.miden::sat::internal::account->internal_account
use.miden::sat::internal::asset
use.miden::sat::account

# CONSTANTS
# =================================================================================================

# Two raised to the power of 32 (2^32)
const.TWO_POW_32=4294967296

# PROCEDURES
# =================================================================================================

#! Builds a fungible asset for the specified fungible faucet and amount.
#!
#! Stack: [faucet_id, amount]
#! Output: [ASSET]
#!
#! - faucet_id is the faucet to create the asset for.
#! - amount is the amount of the asset to create.
#! - ASSET is the built fungible asset.
export.build_fungible_asset
    # assert the faucet is a fungible faucet
    dup exec.internal_account::is_fungible_faucet assert
    # => [faucet_id, amount]

    # assert the amount is valid
    dup.1 exec.asset::get_fungible_asset_max_amount lte assert
    # => [faucet_id, amount]

    # create the asset
    push.0.0 movup.2
    # => [ASSET]
end

#! Creates a fungible asset for the faucet the transaction is being executed against.
#!
#! Stack: [amount]
#! Output: [ASSET]
#!
#! - amount is the amount of the asset to create.
#! - ASSET is the created fungible asset.
export.create_fungible_asset
    # fetch the id of the faucet the transaction is being executed against.
    exec.account::get_id
    # => [id, amount]

    # build the fungible asset
    exec.build_fungible_asset
    # => [ASSET]
end

#! Builds a non fungible asset for the specified non-fungible faucet and amount.
#!
#! Stack: [faucet_id, DATA_HASH]
#! Output: [ASSET]
#!
#! - faucet_id is the faucet to create the asset for.
#! - DATA_HASH is the data hash of the non-fungible asset to build.
#! - ASSET is the built non-fungible asset.
export.build_non_fungible_asset
    # assert the faucet is a non-fungible faucet
    dup exec.internal_account::is_non_fungible_faucet assert
    # => [faucet_id, DATA_HASH]

    # build the asset
    movup.3 drop movdn.2
    # => [hash_0, hash_1, faucet_id, hash_3]

    # set the most significant bit of the asset to 0
    u32split u32wrapping_mul.2 div.2 push.TWO_POW_32 mul add
    # => [ASSET]
end

#! Creates a non-fungible asset for the faucet the transaction is being executed against.
#!
#! Stack: [DATA_HASH]
#! Output: [ASSET]
#!
#! - DATA_HASH is the data hash of the non-fungible asset to create.
#! - ASSET is the created non-fungible asset.
export.create_non_fungible_asset
    # get the id of the faucet the transaction is being executed against
    exec.account::get_id
    # => [id, DATA_HASH]

    # build the non-fungible asset
    exec.build_non_fungible_asset
    # => [ASSET]
end
